group 'com.example'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

apply plugin: 'scala'
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

mainClassName = "com.example.SpnegoServerMain"

dependencies {
    compile 'org.scala-lang:scala-library:2.11.8'
    compile 'com.twitter:finatra-http_2.11:17.12.0'
    compile 'ch.qos.logback:logback-classic:1.1.7'

    testCompile 'org.scalatest:scalatest_2.11:3.0.0'
    testCompile 'junit:junit:4.12'
}

task npmInstall(type: Exec) {
    workingDir 'src/main/webapp'
    commandLine 'npm', 'install'
}

task ngBuild(type: Exec) {
    dependsOn npmInstall

    workingDir 'src/main/webapp'
    commandLine 'ng', 'build'
}

processResources {
    dependsOn ngBuild

    from("src/main/webapp/dist/") {
        into 'public'
    }
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Main-Class': mainClassName
        )
    }
}

task cleanWebappDist(type: Exec) {
    workingDir 'src/main/webapp'
    commandLine 'rm', '-rf', 'dist/'
}

task cleanWebappNpmModule(type: Exec) {
    workingDir 'src/main/webapp'
    commandLine 'rm', '-rf', 'node_modules/'
}

clean {
    dependsOn cleanWebappDist
    dependsOn cleanWebappNpmModule
}